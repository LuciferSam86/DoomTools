/* ------------------------------------------------------------------------ */

#define SRC_DIR_TEXTURES    "/textures"
#define MERGESCRIPT_TEXTURE "scripts/merge-textures.txt"


/* ------------------------------------------------------------------------ */

/**
 * Builds the texture WAD.  
 * Creates texture WAD.
 */
check function doTextures() {
	initBuild();
	texturesDir = getSourceDirectory() + SRC_DIR_TEXTURES;
	verifydirs(texturesDir + "/flats");
	verifydirs(texturesDir + "/patches");
	wadmerge(file(MERGESCRIPT_TEXTURE), [
		getBuildDirectory(),
		getSourceDirectory(),
		getTextureWad()
	]);
}


function exportTextureLump(iwadPath, textureFilePath) {
	if (empty(iwadPath))
		return error("ToolError", "IWAD file not specified.");
	if (empty(textureFilePath))
		return error("ToolError", "Texture listing output file path not specified.");
	if (tool::wadtex({
        sourcefile: file(textureFilePath), 
        wadfile: file(iwadPath), 
        exportmode: true,
        verbose: false 
	})) return error("ToolError", "WADTex threw an error.");
}


/**
 * Rebuilds the texture WAD.  
 * Creates texture listings if they do not exist.
 */
check function doRebuildTextures() {
	initBuild();
	
	baseIwadPath = getIwad();
	if (empty(baseIwadPath))
		return error("NoIWAD", "An IWAD for this project was not set in properties: " + PROP_IWADPATH);
	
	check (err) {
		iwad = wadfile(baseIwadPath);
		texture1exists = iwad->wadentryindex("texture1") != null;
		texture2exists = iwad->wadentryindex("texture2") != null;
	}
	close(iwad);
	if (err) {
		return err;
	}
	
	texturesDir = getSourceDirectory() + SRC_DIR_TEXTURES;
	tex1filename = texturesDir + "/texture1.txt";
	tex2filename = texturesDir + "/texture2.txt";
	
	if (texture1exists) {
		if (fileexists(tex1filename))
			println(tex1filename + " exists. Skipping rebuild.");
		else
			exportTextureLump(baseIwadPath, tex1filename);
	} else {
		println("No TEXTURE1. Skipping rebuild of TEXTURE1.");
	}

	if (texture2exists) {
		if (fileexists(tex2filename))
			println(tex2filename + " exists. Skipping rebuild.");
		else
			exportTextureLump(baseIwadPath, tex2filename);
	} else {
		println("No TEXTURE2. Skipping rebuild of TEXTURE2.");
	}
	
	if (!texture1exists && !texture2exists) {
		println("IWAD does not have texture entries. Can't rebuild.");		
	}

}


