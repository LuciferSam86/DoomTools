/* ------------------------------------------------------------------------ */

/**
 * Return the output textures WAD.
 */
function getTextureWad() {
	return prop(PROP_TEXWAD, DEFAULT_TEXWAD); 
}

/**
 * Return the texture merge script.
 */
function getTexturesScript() {
	return prop(PROP_TEXTURESCRIPT, DEFAULT_TEXTURESCRIPT); 
}

/* ------------------------------------------------------------------------ */

/**
 * Builds the texture WAD.  
 * Creates texture WAD.
 */
check function doTextures() {
	initBuild();
	verifydirs(SRC_DIR_TEXTURES + "/flats");
	verifydirs(SRC_DIR_TEXTURES + "/patches");
	wadmerge(file(getTexturesScript()), [
		getBuildDirectory(), 
		getTextureWad()
	]);
}

/**
 * Reduces the textures to what are used in the maps. 
 * Creates maptex WAD.
 */
function doMapTextures() {
	
	// Only do if maps were compiled.
	if (!fileexists(getBuildDirectory() + "/" + getMapsWad())) {
		println("No maps in `" + (getBuildDirectory() + "/" + getMapsWad()) + "`. Skipping texture extraction.");
		return;
	}

	baseIwadPath = getIwad();	
	if (empty(baseIwadPath))
		return error("NoIWAD", "An IWAD for this project was not set in properties: " + PROP_IWADPATH);

	check (err) extractUsedMapTextures(
		baseIwadPath,
		[getBuildDirectory() + "/" + getMapsWad()],
		[getBuildDirectory() + "/" + getTextureWad()],
		getBuildDirectory() + "/" + getMapTexWad()
	);
	return err;
}


/*****************************************************************************
 * TARGET: textures
 *****************************************************************************
 * Reads the texture assets and merges them into a WAD.
 * doommake textures
 ****************************************************************************/
check entry textures(args) {
	doTextures();
}


/*****************************************************************************
 * TARGET: maptextures
 *****************************************************************************
 * Reads the map WADs and compiled texture resources and exports 
 * them to a WAD of used textures.
 * doommake maptextures
 ****************************************************************************/
check entry maptextures(args) {
	doMapTextures();
}

