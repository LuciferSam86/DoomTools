#define PROP_MAPSWAD "doommake.file.maps"
#define DEFAULT_MAPSWAD "maps.wad"

#define DIR_SRC_MAPS "src/maps"

/**
 * Returns the output map wad.
 */
function getMapsWad() {
	return prop(PROP_MAPSWAD, DEFAULT_MAPSWAD); 
}

/**
 * Scans the project for the maps.
 */
function scanMapList() {
	return filelist(DIR_SRC_MAPS, false, REGEX_WADFILES);
}

/**
 * Merges maps together.
 */
function mergeMaps() {
	verifydirs(DIR_SRC_MAPS);
	
	outwadpath = getBuildDirectory() + "/" + getMapsWad();
	
	mapWads = scanMapList();
	if (empty(mapWads))
		return error("NoMaps", "No map WADs found in: " + DIR_SRC_MAPS);
	
	check (err) {
		destwad = wadfilecreate(outwadpath);
		each (w : mapWads) {
			check (err) {
				srcwad = wadfile(w);
				println("Merging map " + w + "...");
				destWad->wadimport(srcwad, srcwad->wadentries());
			}
			close(srcwad);
			if (err)
				return err;
		}
	}
	close(destwad);
	if (err)
		return err;

	println("Wrote file `" + outwadpath + "`.");
}


/*****************************************************************************
 * TARGET: maps
 *****************************************************************************
 * Merges the maps WAD.
 * doommake maps
 ****************************************************************************/
check entry maps(args) {
	initBuild();
	mergeMaps();
}

